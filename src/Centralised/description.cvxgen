# Produced by CVXGEN, 2019-04-24 04:04:53 -0400.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  n  = 4  # UAV States
  nb = 4  # USV States
  m  = 2  # UAV Inputs
  mb = 2  # USV Inputs
  T = 25  # Horizon
end

# TODO: Consider having different cost matrices for USV and UAV
parameters
  A (n, n)            # UAV Dynamics Matrix
  B (n, m)            # UAV Transfer Matrix
  Ab(nb, nb)          # USV Dynamics Matrix
  Bb(nb, mb)          # USV Transfer Matrix
  Q (n,n) psd         # State Difference Cost
  R (m,m) psd         # Input cost
  P (n,n) psd         # Final State Difference Cost
  x[0] (n)            # Initial UAV State
  xb[0](nb)           # Initial USV State
  vmax nonnegative
  amax   nonnegative
  amin   nonpositive
  amax_b nonnegative
  amin_b nonpositive
end

variables
  x [t] (n),  t=1..T      # UAV State
  xb[t] (nb), t=1..T      # USV State
  u [t] (m),  t=0..T-1    # UAV Control Signal
  ub[t] (mb), t=0..T-1    # USV Control Signal
end

minimize
  sum[t=0..T-1]( quad(x[t]-xb[t], Q) + quad(u[t], R) + quad(ub[t], R) ) + quad(x[T]-xb[T], P)
subject to
  x[t+1]  ==  A*x[t]  +  B*u[t],  t=0..T-1
  xb[t+1] == Ab*xb[t] + Bb*ub[t], t=0..T-1
  # Add bounds to relevant state variables, e.g. max velocity
  amin   <= u[t]   <= amax,   t=0..T-1
  amin_b <= ub[t] <= amax_b, t=0..T-1
  -vmax <= x[t+1][3] <= vmax, t=0..T-1
  -vmax <= x[t+1][4] <= vmax, t=0..T-1
end
