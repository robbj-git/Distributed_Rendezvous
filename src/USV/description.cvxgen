# Produced by CVXGEN, 2019-04-24 04:09:08 -0400.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  n = 4  # States
  m = 2  # Inputs
  T = 45  # Horizon
end

parameters
  A (n, n)            # USV Dynamics Matrix
  B (n, m)            # USV Transfer Matrix
  Q (n,n) psd         # State Difference Cost
  R (m,m) psd         # Input cost
  P (n,n) psd         # Final State Difference Cost
  xb[0] (n)           # Initial USV State
  x [t] (n), t=0..T   # Trajectory of UAV
  vmax nonnegative
  amax nonnegative
  amin nonpositive
  #vmax_sqr nonnegative
end

variables
  xb[t] (n), t=1..T
  ub[t] (m), t=0..T-1
end

minimize
  sum[t=0..T-1]( quad(x[t]-xb[t], Q) + quad(ub[t], R) ) + quad(x[T]-xb[T], P)
subject to
  xb[t+1] == A*xb[t]   + B*ub[t],   t=0..T-1
  # Add bounds to relevant state variables, e.g. max velocity
  amin <= ub[t] <= amax, t=0..T-1
  -vmax <= xb[t+1][3] <= vmax, t=0..T-1
  -vmax <= xb[t+1][4] <= vmax, t=0..T-1
end
